{a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w#}::Indices(position=independent).

;::Symbol;

\partial{#}::PartialDerivative.

\Gamma^{a}_{b c}::TableauSymmetry(shape={2}, indices={1,2}).

Rabcd := R^{a}_{b c d} ->   \partial_{c}{\Gamma^{a}_{b d}}
                          - \partial_{d}{\Gamma^{a}_{b c}}
                          + \Gamma^{e}_{b d} \Gamma^{a}_{c e}
                          - \Gamma^{e}_{b c} \Gamma^{a}_{d e}.

dRabcd := R^{a}_{b c d ; e} -> \partial_{e}{R^{a}_{b c d}}
                             + \Gamma^{a}_{f e} R^{f}_{b c d}
                             - \Gamma^{f}_{b e} R^{a}_{f c d}
                             - \Gamma^{f}_{c e} R^{a}_{b f d}
                             - \Gamma^{f}_{d e} R^{a}_{b c f}.


expr := R^{a}_{b c d} + R^{a}_{b d c}.

substitute (expr, Rabcd)

expr := R^{a}_{b c d} + R^{a}_{d b c} + R^{a}_{c d b}.

substitute   (expr, Rabcd)
canonicalise (expr)

expr := R^{a}_{b c d ; e} + R^{a}_{b e c ; d} + R^{a}_{b d e ; c}.

substitute     (expr, dRabcd)
substitute     (expr,  Rabcd)
distribute     (expr)
product_rule   (expr)
sort_product   (expr)
rename_dummies (expr)
canonicalise   (expr)

