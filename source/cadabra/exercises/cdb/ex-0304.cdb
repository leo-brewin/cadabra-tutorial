{a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w#}::Indices(position=independent).

\partial{#}::PartialDerivative.

g_{a b}::Symmetric.
g^{a b}::Symmetric.

\Gamma^{a}_{b c}::TableauSymmetry(shape={2}, indices={1,2}).
\Gamma_{a b c}::TableauSymmetry(shape={2}, indices={1,2}).

GammaU := \Gamma^{a}_{b c} ->  1/2 g^{a d} (   \partial_{b}{g_{d c}}
                                             + \partial_{c}{g_{b d}}
                                             - \partial_{d}{g_{b c}}).

GammaD := \Gamma_{a b c} ->  1/2 (   \partial_{b}{g_{a c}}
                                   + \partial_{c}{g_{b a}}
                                   - \partial_{a}{g_{b c}}).

Rabcd := R_{a b c d} ->   \partial_{c}{\Gamma_{a b d}}
                        - \partial_{d}{\Gamma_{a b c}}
                        + \Gamma_{e a d} \Gamma^{e}_{b c}
                        - \Gamma_{e a c} \Gamma^{e}_{b d}.


expr := R_{a b c d} + R_{b a c d}.

substitute     (expr, Rabcd)
substitute     (expr, GammaU)
substitute     (expr, GammaD)
distribute     (expr)
product_rule   (expr)
sort_product   (expr)
rename_dummies (expr)
canonicalise   (expr)

expr := R_{a b c d} - R_{c d a b}.

substitute     (expr, Rabcd)
substitute     (expr, GammaU)
substitute     (expr, GammaD)
distribute     (expr)
product_rule   (expr)
sort_product   (expr)
rename_dummies (expr)
canonicalise   (expr)

