{a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u,v,w#}::Indices(position=independent).

;::Symbol;

\partial{#}::PartialDerivative.

\Gamma^{a}_{b c}::TableauSymmetry(shape={2}, indices={1,2}).

RabcdD :=   \partial_{c}{\Gamma_{a b d}}
          - \partial_{d}{\Gamma_{a b c}}
          + \Gamma_{e a d} \Gamma^{e}_{b c}
          - \Gamma_{e a c} \Gamma^{e}_{b d} -> R_{a b c d}.

RabcdU :=   \partial_{c}{\Gamma^{a}_{b d}}
          - \partial_{d}{\Gamma^{a}_{b c}}
          + \Gamma^{e}_{b d} \Gamma^{a}_{c e}
          - \Gamma^{e}_{b c} \Gamma^{a}_{d e} -> R^{a}_{b c d}.

d1Rabcd := R_{a b c d ; e} ->   \partial_{e}{R_{a b c d}}
                              - \Gamma^{f}_{a e} R_{f b c d}
                              - \Gamma^{f}_{b e} R_{a f c d}
                              - \Gamma^{f}_{c e} R_{a b f d}
                              - \Gamma^{f}_{d e} R_{a b c f}.

d2Rabcd := R_{a b c d ; e ; f} ->   \partial_{f}{R_{a b c d ; e}}
                                  - \Gamma^{g}_{a f} R_{g b c d ; e}
                                  - \Gamma^{g}_{b f} R_{a g c d ; e}
                                  - \Gamma^{g}_{c f} R_{a b g d ; e}
                                  - \Gamma^{g}_{d f} R_{a b c g ; e}
                                  - \Gamma^{g}_{e f} R_{a b c d ; g}.

substitute (d2Rabcd,d1Rabcd)

expr := R_{a b c d ; e ; f} - R_{a b c d ; f ; e}.

substitute     (expr,d2Rabcd)

distribute     (expr)
product_rule   (expr)

sort_product   (expr)
rename_dummies (expr)
canonicalise   (expr)
factor_out     (expr,$R_{a? b? c? d?}$)

substitute     (expr,RabcdU)
substitute     (expr,$R^{a}_{b c d} -> -R^{a}_{b d c}$)


