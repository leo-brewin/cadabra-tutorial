{t, r, \theta, \varphi}::Coordinate.
{a,b,c,d,e,f,g,h#}::Indices(values={t, r, \theta, \varphi}, position=independent).

;::Symbol.

\partial{#}::PartialDerivative.

g_{a b}::Metric.
g^{a b}::InverseMetric.

Gamma := \Gamma^{a}_{f g} -> 1/2 g^{a b} (   \partial_{g}{g_{b f}}
                                           + \partial_{f}{g_{b g}}
                                           - \partial_{b}{g_{f g}} ).

deriv := \xi_{a ; b} -> \partial_{b}{\xi_{a}} - \Gamma^{c}_{a b} \xi_{c}.
lower := \xi_{a} -> g_{a b} \xi^{b}.

expr  := \xi_{a ; b} + \xi_{b ; a}.

substitute   (expr, deriv)
substitute   (expr, lower)
substitute   (expr, Gamma)
distribute   (expr)
product_rule (expr)
canonicalise (expr)

# choose a vector

# Kvect := {\xi^{t} = 1}.
# Kvect := {\xi^{\varphi} = 1}.
Kvect := {\xi^{\theta} = \sin(\varphi), \xi^{\varphi} = \cos(\theta)/\sin(\theta) \cos(\varphi)}.
# Kvect := {\xi^{\theta} = \cos(\varphi), \xi^{\varphi} = - \cos(\theta)/\sin(\theta) \sin(\varphi)}.

gab := { g_{t t}            = -(1-2*m/r),
         g_{r r}            = 1/(1-(2*m/r)),
         g_{\theta\theta}   = r**2,
         g_{\varphi\varphi} = r**2 \sin(\theta)**2}.

complete   (gab, $g^{a b}$)

evaluate   (expr, gab+Kvect)

