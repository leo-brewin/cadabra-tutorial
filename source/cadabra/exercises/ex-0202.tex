\documentclass[12pt]{cdblatex}
\usepackage{exercises}

\begin{document}

% --------------------------------------------------------------------------------------------
\section*{Exercise 2.2 Covariant derivative of $v_{ab}$}

\begin{cadabra}
   {a,b,c,d,e,f,g,h,i,j,k,l,m,n,o,p,q,r,s,t,u#}::Indices(position=independent).

   \nabla{#}::Derivative.
   \partial{#}::PartialDerivative.

   # template for covariant derivative of a vector

   derivU := \nabla_{a}{A?^{b}} -> \partial_{a}{A?^{b}} + \Gamma^{b}_{c a} A?^{c}.
   derivD := \nabla_{a}{A?_{b}} -> \partial_{a}{A?_{b}} - \Gamma^{c}_{b a} A?_{c}.

   vab := v_{a b} -> A_{a} B_{b}.
   iab := A_{a} B_{b} -> v_{a b}.

   pab := \partial_{a}{A_{b}} B_{c} -> \partial_{a}{A_{b} B_{c}} - A_{b} \partial_{a}{B_{c}}.

   # create an object

   Dvab := \nabla_{a}{v_{b c}}.     # cdb (ex-0202.101,Dvab)

   # apply the rule, then simplify

   substitute     (Dvab,vab)        # cdb (ex-0202.102,Dvab)
   product_rule   (Dvab)            # cdb (ex-0202.103,Dvab)
   substitute     (Dvab,derivD)     # cdb (ex-0202.104,Dvab)
   substitute     (Dvab,derivU)     # cdb (ex-0202.105,Dvab)
   distribute     (Dvab)            # cdb (ex-0202.106,Dvab)
   substitute     (Dvab,pab)        # cdb (ex-0202.107,Dvab)
   canonicalise   (Dvab)            # cdb (ex-0202.108,Dvab)
   substitute     (Dvab,iab)        # cdb (ex-0202.109,Dvab)
   sort_product   (Dvab)            # cdb (ex-0202.110,Dvab)
\end{cadabra}

\begin{align}
   \cdb{ex-0202.101} &= \Cdb{ex-0202.102}\\
                     &= \Cdb{ex-0202.103}\\
                     &= \Cdb{ex-0202.104}\\
                     &= \Cdb{ex-0202.105}\\
                     &= \Cdb{ex-0202.106}\\
                     &= \Cdb{ex-0202.107}\\
                     &= \Cdb{ex-0202.108}\\
                     &= \Cdb{ex-0202.109}\\
                     &= \Cdb{ex-0202.110}
\end{align}

\end{document}
